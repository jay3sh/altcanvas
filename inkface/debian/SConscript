
import os

Import('VERSION ARCH MAINTAINER DEPENDS DESCRIPTION PKGNAME')
Import('env')

svn_version = os.popen('svnversion ..').read()[:-1]
# This may be as simple as '89' or as complex as '4123:4184M'.
# We'll just use the last bit.
svn_version = svn_version.split(':')[-1]


debpkg = '#%s_%s_%s.deb'%('inkface',VERSION,ARCH)

DEBFILES = [
    ("usr/lib/libinkface.so", "#src/libinkface.so"),
    ("usr/include/inkface.h", "#src/inkface.h"),
    ("usr/lib/pkgconfig/inkface.pc","#src/inkface.pc"),
]

PKGDIR = 'tmpdeb'

DEBCONTROLFILE = os.path.join(PKGDIR, "DEBIAN/control")

for f in DEBFILES:

    dest = os.path.join(PKGDIR,f[0])

    env.Depends(debpkg,dest)

    env.Command(dest,f[1],Copy('$TARGET','$SOURCE'))

    env.Depends(DEBCONTROLFILE, dest)


CONTROL_TEMPLATE = """
Package: %s
Priority: extra
Section: misc
Installed-Size: %s
Maintainer: %s
Architecture: %s
Version: %s-%s
Depends: %s
Description: %s

"""
env.Depends(debpkg,DEBCONTROLFILE )

# This function creates the control file from the template and info
# specified above, and works out the final size of the package.
def make_control(target=None, source=None, env=None):
    installed_size = 0
    for i in DEBFILES:
        installed_size += os.stat(str(env.File(i[1])))[6]
        control_info = CONTROL_TEMPLATE % (
            PKGNAME, installed_size, MAINTAINER, ARCH, VERSION,
            svn_version, DEPENDS, DESCRIPTION)
        f = open(str(target[0]), 'w')
        f.write(control_info)
        f.close()

env.Command(DEBCONTROLFILE, None, make_control)

env.Command(debpkg, DEBCONTROLFILE,
            "dpkg-deb -b %s %s"%(PKGDIR,"$TARGET"))

Delete(PKGDIR)
